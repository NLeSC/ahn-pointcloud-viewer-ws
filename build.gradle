plugins {
    id 'java'
    id 'application'
    // coverage
    id 'jacoco'
    // ide
    id 'idea'
    id 'eclipse'
    // quality
    id 'org.sonarqube' version '2.5'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

version = '1.1.1'
applicationName = 'ahn-pointcloud-viewer-ws'
group = 'nl.esciencecenter.ahn'
mainClassName = 'nl.esciencecenter.ahn.pointcloud.ViewerApplication'

ext {
    xenonVersion = '1.0.0'
    dropwizardVersion = '1.1.4'
    postgresqlVersion = '9.4-1201-jdbc41'
    jschVersion = '0.1.52'
}

repositories {
    mavenCentral()
    maven {
        url 'http://nlesc.github.io/Xenon/repo'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile 'org.mockito:mockito-core:2.+'

    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: dropwizardVersion
    compile group: 'com.smoketurner', name: 'dropwizard-swagger', version: '1.1.1-1'
    compile group: 'org.postgresql', name: 'postgresql', version: postgresqlVersion
    compile group: 'nl.esciencecenter', name: 'xenon', version: xenonVersion

    // deps for Xenon
    compile group: 'com.jcraft', name: 'jsch', version: jschVersion
}

run {
    args 'server', 'config.yml'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

// include Python scripts in distribution
distributions {
    main {
        contents {
            from(file('src/main/python')) {
                into "python"
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'nlesc'
        property 'sonar.language', 'java'
        property 'sonar.links.homepage', 'https://github.com/NLeSC/ahn-pointcloud-viewer-ws'
    }
}
